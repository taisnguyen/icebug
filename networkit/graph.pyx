# distutils: language=c++

from cython.operator import dereference, preincrement
from libcpp.algorithm cimport swap

import numpy as np
from scipy.sparse import coo_matrix
cimport numpy as cnp
cnp.import_array()

# PyArrow imports
import pyarrow as pa

from .base import Algorithm
from .helpers import stdstring, pystring
from .traversal import Traversal
from . import graphio
import os

cdef class Graph:

	"""
	Graph(n=0, weighted=False, directed=False, edgesIndexed=False)

	An undirected graph (with optional weights) and parallel iterator methods.

	Create a graph of `n` nodes. The graph has assignable edge weights if `weighted` is set to True.
	If `weighted` is set to False each edge has edge weight 1.0 and any other weight assignment will
	be ignored.

	Parameters
	----------
	n : int, optional
		Number of nodes. Default: 0
	weighted : bool, optional
		If set to True, the graph can have edge weights other than 1.0. Default: False
	directed : bool, optional
		If set to True, the graph will be directed. Default: False
	edgesIndexed : bool, optional
		If set to True, the graph's edges will be indexed. Default: False
	"""

	def __cinit__(self, n=0, bool_t weighted=False, bool_t directed=False, bool_t edgesIndexed=False):
		if isinstance(n, Graph):
			self._this = make_shared[_GraphW](dereference((<Graph>n)._this), weighted, directed, edgesIndexed)
		else:
			self._this = make_shared[_GraphW](<count>n, weighted, directed, edgesIndexed)

		# Keep Arrow arrays alive for CSR graphs
		self._arrow_arrays = {}

	cdef setThis(self, _Graph& other):
		self._this = make_shared[_GraphW](other)
		return self

	cdef setThisFromGraphW(self, _GraphW& other):
		self._this = make_shared[_GraphW](other)
		return self

	def __copy__(self):
		"""
		Generates a copy of the graph
		"""
		return Graph().setThis(dereference(self._this))

	def __deepcopy__(self, memo):
		"""
		Generates a (deep) copy of the graph
		"""
		return Graph().setThis(dereference(self._this))

	def __str__(self):
		return "NetworKit.Graph(n={0}, m={1})".format(self.numberOfNodes(), self.numberOfEdges())

	def __getstate__(self):
		return graphio.NetworkitBinaryWriter(graphio.Format.NetworkitBinary, chunks = 32, weightsType = 5).writeToBuffer(self)

	def __setstate__(self, state):
		newG = graphio.NetworkitBinaryReader().readFromBuffer(state)
		self._this = make_shared[_GraphW](dereference((<Graph>newG)._this), <bool_t>(newG.isWeighted()), <bool_t>(newG.isDirected()), <bool_t>(newG.hasEdgeIds()))

	@classmethod
	def fromCSR(cls, count n, bool_t directed, out_indices, out_indptr, in_indices=None, in_indptr=None, out_weights=None, in_weights=None):
		"""
		fromCSR(n, directed, out_indices, out_indptr, in_indices=None, in_indptr=None, out_weights=None, in_weights=None)

		Create a graph from CSR (Compressed Sparse Row) arrays using zero-copy Arrow arrays.

		Parameters
		----------
		n : int
			Number of nodes
		directed : bool
			If True, the graph will be directed
		out_indices : pyarrow.Array
			CSR indices array containing neighbor node IDs for outgoing edges
		out_indptr : pyarrow.Array
			CSR indptr array containing offsets into out_indices for each node
		in_indices : pyarrow.Array, optional
			CSR indices array for incoming edges (only needed for directed graphs)
		in_indptr : pyarrow.Array, optional
			CSR indptr array for incoming edges (only needed for directed graphs)
		out_weights : pyarrow.Array, optional
			CSR edge weights array for outgoing edges
		in_weights : pyarrow.Array, optional
			CSR edge weights array for incoming edges (only needed for directed graphs)

		Returns
		-------
		Graph
			A new Graph instance using CSR storage
		"""
		import pyarrow as pa

		# Declare all variables at the beginning
		cdef CResult[shared_ptr[CArray]] out_indices_result
		cdef CResult[shared_ptr[CArray]] out_indptr_result
		cdef CResult[shared_ptr[CArray]] in_indices_result
		cdef CResult[shared_ptr[CArray]] in_indptr_result
		cdef CResult[shared_ptr[CArray]] out_weights_result
		cdef CResult[shared_ptr[CArray]] in_weights_result
		cdef shared_ptr[UInt64Array] out_indices_ptr
		cdef shared_ptr[UInt64Array] out_indptr_ptr
		cdef shared_ptr[UInt64Array] in_indices_ptr
		cdef shared_ptr[UInt64Array] in_indptr_ptr
		cdef shared_ptr[DoubleArray] out_weights_ptr
		cdef shared_ptr[DoubleArray] in_weights_ptr
		cdef Graph result

		# Ensure arrays are UInt64Arrays
		if not isinstance(out_indices, pa.UInt64Array):
			out_indices = pa.array(out_indices, type=pa.uint64())
		if not isinstance(out_indptr, pa.UInt64Array):
			out_indptr = pa.array(out_indptr, type=pa.uint64())

		# Convert using C Data Interface - keep Python objects alive
		# Get C interface data and keep references alive
		out_indices_c_data = out_indices.__arrow_c_array__()
		out_indices_schema_capsule, out_indices_array_capsule = out_indices_c_data

		out_indptr_c_data = out_indptr.__arrow_c_array__()
		out_indptr_schema_capsule, out_indptr_array_capsule = out_indptr_c_data

		# Import arrays from C Data Interface
		out_indices_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(out_indices_array_capsule, "arrow_array"),
		                                  <ArrowSchema*>PyCapsule_GetPointer(out_indices_schema_capsule, "arrow_schema"))
		if not out_indices_result.ok():
			error_msg = f"Failed to import out_indices array: {out_indices_result.status().ToString().decode()}"
			raise RuntimeError(error_msg)
		out_indices_ptr = static_pointer_cast[UInt64Array, CArray](out_indices_result.ValueOrDie())

		out_indptr_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(out_indptr_array_capsule, "arrow_array"),
		                                <ArrowSchema*>PyCapsule_GetPointer(out_indptr_schema_capsule, "arrow_schema"))
		if not out_indptr_result.ok():
			error_msg = f"Failed to import out_indptr array: {out_indptr_result.status().ToString().decode()}"
			raise RuntimeError(error_msg)
		out_indptr_ptr = static_pointer_cast[UInt64Array, CArray](out_indptr_result.ValueOrDie())

		# Handle incoming arrays for directed graphs
		if directed and in_indices is not None and in_indptr is not None:
			if not isinstance(in_indices, pa.UInt64Array):
				in_indices = pa.array(in_indices, type=pa.uint64())
			if not isinstance(in_indptr, pa.UInt64Array):
				in_indptr = pa.array(in_indptr, type=pa.uint64())

			# Convert using C Data Interface - keep Python objects alive
			in_indices_c_data = in_indices.__arrow_c_array__()
			in_indices_schema_capsule, in_indices_array_capsule = in_indices_c_data

			in_indptr_c_data = in_indptr.__arrow_c_array__()
			in_indptr_schema_capsule, in_indptr_array_capsule = in_indptr_c_data

			# Import arrays from C Data Interface
			in_indices_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(in_indices_array_capsule, "arrow_array"),
			                                <ArrowSchema*>PyCapsule_GetPointer(in_indices_schema_capsule, "arrow_schema"))
			if not in_indices_result.ok():
				error_msg = f"Failed to import in_indices array: {in_indices_result.status().ToString().decode()}"
				raise RuntimeError(error_msg)
			in_indices_ptr = static_pointer_cast[UInt64Array, CArray](in_indices_result.ValueOrDie())

			in_indptr_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(in_indptr_array_capsule, "arrow_array"),
			                               <ArrowSchema*>PyCapsule_GetPointer(in_indptr_schema_capsule, "arrow_schema"))
			if not in_indptr_result.ok():
				error_msg = f"Failed to import in_indptr array: {in_indptr_result.status().ToString().decode()}"
				raise RuntimeError(error_msg)
			in_indptr_ptr = static_pointer_cast[UInt64Array, CArray](in_indptr_result.ValueOrDie())

		# Handle outgoing weights
		if out_weights is not None:
			if not isinstance(out_weights, pa.DoubleArray):
				out_weights = pa.array(out_weights, type=pa.float64())

			# Convert using C Data Interface - keep Python objects alive
			out_weights_c_data = out_weights.__arrow_c_array__()
			out_weights_schema_capsule, out_weights_array_capsule = out_weights_c_data

			# Import array from C Data Interface
			out_weights_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(out_weights_array_capsule, "arrow_array"),
			                                <ArrowSchema*>PyCapsule_GetPointer(out_weights_schema_capsule, "arrow_schema"))
			if not out_weights_result.ok():
				error_msg = f"Failed to import out_weights array: {out_weights_result.status().ToString().decode()}"
				raise RuntimeError(error_msg)
			out_weights_ptr = static_pointer_cast[DoubleArray, CArray](out_weights_result.ValueOrDie())

		# Handle incoming weights for directed graphs
		if directed and in_weights is not None:
			if not isinstance(in_weights, pa.DoubleArray):
				in_weights = pa.array(in_weights, type=pa.float64())

			# Convert using C Data Interface - keep Python objects alive
			in_weights_c_data = in_weights.__arrow_c_array__()
			in_weights_schema_capsule, in_weights_array_capsule = in_weights_c_data

			# Import array from C Data Interface
			in_weights_result = ImportArray(<ArrowArray*>PyCapsule_GetPointer(in_weights_array_capsule, "arrow_array"),
			                               <ArrowSchema*>PyCapsule_GetPointer(in_weights_schema_capsule, "arrow_schema"))
			if not in_weights_result.ok():
				error_msg = f"Failed to import in_weights array: {in_weights_result.status().ToString().decode()}"
				raise RuntimeError(error_msg)
			in_weights_ptr = static_pointer_cast[DoubleArray, CArray](in_weights_result.ValueOrDie())

		# Create Graph using Arrow CSR constructor
		result = Graph.__new__(Graph)

		# Store Arrow arrays in the graph to keep them alive
		result._arrow_arrays = {
			'out_indices': out_indices,
			'out_indptr': out_indptr,
		}
		if directed and in_indices is not None and in_indptr is not None:
			result._arrow_arrays['in_indices'] = in_indices
			result._arrow_arrays['in_indptr'] = in_indptr
		if out_weights is not None:
			result._arrow_arrays['out_weights'] = out_weights
		if directed and in_weights is not None:
			result._arrow_arrays['in_weights'] = in_weights

		# Force the correct constructor by explicitly casting parameters
		result._this = make_shared[_GraphR](
			<count>n,
			<bool_t>directed,
			<shared_ptr[UInt64Array]>out_indices_ptr,
			<shared_ptr[UInt64Array]>out_indptr_ptr,
			<shared_ptr[UInt64Array]>in_indices_ptr,
			<shared_ptr[UInt64Array]>in_indptr_ptr,
			<shared_ptr[DoubleArray]>out_weights_ptr,
			<shared_ptr[DoubleArray]>in_weights_ptr
		)

		return result

	@classmethod
	def fromIcebugMemGraph(cls, graph):
		"""
		fromIcebugMemGraph(graph)

		Create a Graph from an :class:`icebug_format.IcebugMemGraph`.

		Extracts the ``indices`` and ``indptr`` Arrow tables from *graph* and
		delegates to :meth:`fromCSR` for zero-copy construction.

		The neighbor column is resolved from ``graph.indices`` by preferring a
		column named ``'target'``; if no such column exists the first column is
		used instead.  The row-pointer column is always taken from the first
		column of ``graph.indptr``.

		Parameters
		----------
		graph : icebug_format.IcebugMemGraph
			Arrow CSR graph representation

		Returns
		-------
		Graph
			A new Graph instance backed by the Arrow arrays from *graph*.

		Raises
		------
		TypeError
			If *graph* is missing a required attribute (``src``, ``indices``,
			or ``indptr``), or if ``indices`` / ``indptr`` are not
			:class:`pyarrow.Table` instances.
		ValueError
			If ``graph.indices`` or ``graph.indptr`` contain no columns.
		"""
		for attr in ('src', 'indices', 'indptr'):
			if not hasattr(graph, attr):
				raise TypeError(
					f"graph must be an IcebugMemGraph; missing required attribute '{attr}'"
				)
        
        if not isinstance(graph.src, pa.Table):
			raise TypeError(
				f"graph.src must be a pyarrow.Table, got {type(graph.src).__name__}"
			)
        
		if not isinstance(graph.indices, pa.Table):
			raise TypeError(
				f"graph.indices must be a pyarrow.Table, got {type(graph.indices).__name__}"
			)
        
		if not isinstance(graph.indptr, pa.Table):
			raise TypeError(
				f"graph.indptr must be a pyarrow.Table, got {type(graph.indptr).__name__}"
			)
        
		if graph.indices.num_columns == 0:
			raise ValueError("graph.indices must have at least one column")
		if graph.indptr.num_columns == 0:
			raise ValueError("graph.indptr must have at least one column")

		n = graph.src.num_rows

		# Prefer the 'target' column; fall back to the first column
		if 'target' in graph.indices.column_names:
			out_indices = graph.indices.column('target').cast(pa.uint64())
		else:
			out_indices = graph.indices.column(0).cast(pa.uint64())

		# Always use the first column of indptr
		out_indptr = graph.indptr.column(0).cast(pa.uint64())

        # IcebugMemGraph is always directed
		return cls.fromCSR(n, True, out_indices, out_indptr)

	def hasEdgeIds(self):
		"""
		hasEdgeIds()

		Returns true if edges have been indexed

		Returns
		-------
		bool
			If edges have been indexed
		"""
		return dereference(self._this).hasEdgeIds()

	def edgeId(self, node u, node v):
		"""
		edgeId(u, v)

		Parameters
		----------
		u: node
			Node Id from u.
		v: node
			Node Id from v.

		Returns
		-------
		int
			Id of the edge.
		"""
		return dereference(self._this).edgeId(u, v)

	def numberOfNodes(self):
		"""
		numberOfNodes()

		Get the number of nodes in the graph.

		Returns
		-------
		int
			The number of nodes.
		"""
		return dereference(self._this).numberOfNodes()

	def numberOfEdges(self):
		"""
		numberOfEdges()

		Get the number of edges in the graph.

		Returns
	 	-------
		int
			The number of edges.
		"""
		return dereference(self._this).numberOfEdges()

	def upperNodeIdBound(self):
		"""
		upperNodeIdBound()

		Get an upper bound for the node ids in the graph.

		Returns
		-------
		int
			An upper bound for the node ids in the graph.
		"""
		return dereference(self._this).upperNodeIdBound()

	def upperEdgeIdBound(self):
		"""
		upperEdgeIdBound()

		Get an upper bound for the edge ids in the graph.

		Returns
		-------
		int
			An upper bound for the edge ids in the graph.
		"""
		return dereference(self._this).upperEdgeIdBound()

	def degree(self, u):
		"""
		degree(u)

		Get the number of neighbors of `u`.

		Note
		----
		The existence of the node is not checked. Calling this function with a non-existing node results in a segmentation fault.
		Node existence can be checked by calling hasNode(u).

		Parameters
		----------
		u : int
			The input Node.

		Returns
		-------
		int
			The number of neighbors.
		"""
		return dereference(self._this).degree(u)

	def degreeIn(self, u):
		"""
		degreeIn(u)

		Get the number of in-neighbors of `u`.

		Note
		----
		The existence of the node is not checked. Calling this function with a non-existing node results in a segmentation fault.
		Node existence can be checked by calling hasNode(u).

		Parameters
		----------
		u : int
			The input Node.

		Returns
		-------
		int
			The number of in-neighbors.
		"""
		return dereference(self._this).degreeIn(u)

	def degreeOut(self, u):
		"""
		degreeOut(u)

		Get the number of out-neighbors of `u`.

		Note
		----
		The existence of the node is not checked. Calling this function with a non-existing node results in a segmentation fault.
		Node existence can be checked by calling hasNode(u).

		Parameters
		----------
		u : int
			The Input Node.i
		Returns
		-------
		int
			The number of out-neighbors.
		"""
		return dereference(self._this).degreeOut(u)

	def weightedDegree(self, u, countSelfLoopsTwice=False):
		"""
		weightedDegree(u, countSelfLoopsTwice=False)

		Returns the weighted out-degree of u.

		For directed graphs this is the sum of weights of all outgoing edges of u.

		Parameters
		----------
		u : int
			The input Node.
		countSelfLoopsTwice : bool, optional
			If set to True, self-loops will be counted twice. Default: False

		Returns
		-------
		float
			The weighted out-degree of u.
		"""
		return dereference(self._this).weightedDegree(u, countSelfLoopsTwice)

	def weightedDegreeIn(self, u, countSelfLoopsTwice=False):
		"""
		weightedDegreeIn(u, countSelfLoopsTwice=False)

		Returns the weighted in-degree of u.

		For directed graphs this is the sum of weights of all ingoing edges of u.

		Parameters
		----------
		u : int
			The input node.
		countSelfLoopsTwice : bool, optional
			If set to True, self-loops will be counted twice. Default: False

		Returns
		-------
		float
			The weighted in-degree of u.
		"""
		return dereference(self._this).weightedDegreeIn(u, countSelfLoopsTwice)

	def isIsolated(self, u):
		"""
		isIsolated(u)

		If the node `u` is isolated.

		Parameters
		----------
		u : int
			The input node.

		Returns
		-------
		bool
			Indicates whether the node is isolated.
		"""
		return dereference(self._this).isIsolated(u)

	def hasNode(self, u):
		"""
		hasNode(u)

		Checks if the Graph has the node `u`, i.e. if `u` hasn't been deleted and is in the range of valid ids.

		Parameters
		----------
		u : int
			Id of node queried.

		Returns
		-------
		bool
			Indicates whether node `u` is part of the graph.
		"""
		return dereference(self._this).hasNode(u)

	def hasEdge(self, u, v):
		"""
		hasEdge(u, v)

		Checks if undirected edge {`u`,`v`} exists in the graph.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.

		Returns
		-------
		bool
			True if the edge exists, False otherwise.
		"""
		return dereference(self._this).hasEdge(u, v)

	def weight(self, u, v):
		"""
		weight(u, v)

		Get edge weight of edge {`u` , `v`}. Returns 0 if edge does not exist.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.

		Returns
		-------
		float
			Edge weight of edge {`u` , `v`} or 0 if edge does not exist.
		"""
		return dereference(self._this).weight(u, v)

	def forNodes(self, object callback):
		"""
		forNodes(callback)

		Experimental node iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameter node.
		"""
		cdef NodeCallbackWrapper* wrapper
		try:
			wrapper = new NodeCallbackWrapper(callback)
			dereference(self._this).forNodes[NodeCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forNodesInRandomOrder(self, object callback):
		"""
		forNodesInRandomOrder(callback)

		Experimental node iterator interface

		Parameters:
		-----------
		callback : object
			Any callable object that takes the parameter node.
		"""
		cdef NodeCallbackWrapper* wrapper
		try:
			wrapper = new NodeCallbackWrapper(callback)
			dereference(self._this).forNodesInRandomOrder[NodeCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forNodePairs(self, object callback):
		"""
		forNodePairs(callback)

		Experimental node pair iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameters tuple(int, int).
			Parameter list refering to (node id, node id).
		"""
		cdef NodePairCallbackWrapper* wrapper
		try:
			wrapper = new NodePairCallbackWrapper(callback)
			dereference(self._this).forNodePairs[NodePairCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forEdges(self, object callback):
		"""
		forEdges(callback)

		Experimental edge iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			dereference(self._this).forEdges[EdgeCallBackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forEdgesOf(self, node u, object callback):
		"""
		forEdgesOf(u, callback)

		Experimental incident (outgoing) edge iterator interface

		Parameters
		----------
		u : int
			The node of which incident edges shall be passed to the callback
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			dereference(self._this).forEdgesOf[EdgeCallBackWrapper](u, dereference(wrapper))
		finally:
			del wrapper

	def forInEdgesOf(self, node u, object callback):
		"""
		forInEdgesOf(u, callback)

		Experimental incident edge iterator interface

		Parameters
		----------
		u : int
			The node of which incident edges shall be passed to the callback
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			dereference(self._this).forInEdgesOf[EdgeCallBackWrapper](u, dereference(wrapper))
		finally:
			del wrapper

	def isWeighted(self):
		"""
		isWeighted()

		Returns whether a graph is weighted.

		Returns
		-------
		bool
			True if this graph supports edge weights other than 1.0.
		"""
		return dereference(self._this).isWeighted()

	def isDirected(self):
		"""
		isDirected()

		Returns whether a graph is directed.

		Returns
		-------
		bool
			True if graph is directed.
		"""
		return dereference(self._this).isDirected()

	def totalEdgeWeight(self):
		"""
		totalEdgeWeight()

		Get the sum of all edge weights.

		Returns
		-------
		float
			The sum of all edge weights.
		"""
		return dereference(self._this).totalEdgeWeight()

	def numberOfSelfLoops(self):
		"""
		numberOfSelfLoops()

		Get number of self-loops, i.e. edges {v, v}.

		Returns
		-------
		int
			Number of self-loops.
		"""
		return dereference(self._this).numberOfSelfLoops()

	def checkConsistency(self):
		"""
		checkConsistency()

		Check for invalid graph states, such as multi-edges.

		Returns
		-------
		bool
			True if graph contains invalid graph states.
		"""
		return dereference(self._this).checkConsistency()

	def iterNodes(self):
		"""
		iterNodes()

		Iterates over the nodes of the graph.
		"""
		cdef _NodeRange node_range = dereference(self._this).nodeRange()
		cdef _NodeIterator it = node_range.begin()
		cdef _NodeIterator end_it = node_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterEdges(self):
		"""
		iterEdges()

		Iterates over the edges of the graph.

		For each node u in the graph in ascending node id order,
		the iterator yields the out-edges of u in directed graphs
		and the edges (u,v) in which u < v for undirected graphs.

		It does not follow the order of edge ids (if present).
		"""
		cdef _EdgeRange edge_range = dereference(self._this).edgeRange()
		cdef _EdgeIterator it = edge_range.begin()
		cdef _EdgeIterator end_it = edge_range.end()
		while it != end_it:
			yield dereference(it).u, dereference(it).v
			preincrement(it)

	def iterEdgesWeights(self):
		"""
		iterEdgeWeights()

		Iterates over the edges of the graph and their weights.
		"""
		cdef _EdgeWeightRange edge_weight_range = dereference(self._this).edgeWeightRange()
		cdef _EdgeWeightIterator it = edge_weight_range.begin()
		cdef _EdgeWeightIterator end_it = edge_weight_range.end()
		while it != end_it:
			yield dereference(it).u, dereference(it).v, dereference(it).weight
			preincrement(it)

	def iterNeighbors(self, u):
		"""
		iterNeighbors(u)

		Iterates over the neighbors of a node.

		Parameters
		----------
		u : int
			The input node.
		"""
		cdef _OutNeighborRange neighbor_range = dereference(self._this).neighborRange(u)
		cdef _NeighborIterator it = neighbor_range.begin()
		cdef _NeighborIterator end_it = neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterInNeighbors(self, u):
		"""
		iterInNeighbors(u)

		Iterates over the in-neighbors of a node.

		Parameters
		----------
		u : int
			The input node.
		"""
		cdef _InNeighborRange in_neighbor_range = dereference(self._this).inNeighborRange(u)
		cdef _NeighborIterator it = in_neighbor_range.begin()
		cdef _NeighborIterator end_it = in_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterNeighborsWeights(self, u):
		"""
		iterNeighborsWeights(u)

		Iterates over a range of the neighbors of a node including the edge weights.
		The iterator is not safe to use with unweighted graphs. To avoid unsafe behavior
		a runtime error will be thrown.

		Parameters
		----------
		u : int
			The input node.
		"""
		if not dereference(self._this).isWeighted():
			raise RuntimeError("iterNeighborsWeights: Use this iterator only on weighted graphs.")

		cdef _OutNeighborWeightRange weight_neighbor_range = dereference(self._this).weightNeighborRange(u)
		cdef _NeighborWeightIterator it = weight_neighbor_range.begin()
		cdef _NeighborWeightIterator end_it = weight_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterInNeighborsWeights(self, u):
		"""
		iterInNeighborsWeights(u)

		Iterates over a range of the in-neighbors of a node including the edge weights.
		The iterator is not safe to use with unweighted graphs. To avoid unsafe behavior
		a runtime error will be thrown.

		Parameters
		----------
		u : int
			The input node.
		"""
		if not dereference(self._this).isWeighted():
			raise RuntimeError("iterInNeighborsWeights: Use this iterator only on weighted graphs.")

		cdef _InNeighborWeightRange weight_in_neighbor_range = dereference(self._this).weightInNeighborRange(u)
		cdef _NeighborWeightIterator it = weight_in_neighbor_range.begin()
		cdef _NeighborWeightIterator end_it = weight_in_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def attachNodeAttribute(self, name, ofType):
		"""
		attachNodeAttribute(name, ofType)

		Attaches a node attribute to the graph and returns it.

		.. code-block::

			A = G.attachNodeAttribute("attributeIdentifier", ofType)

		All values are initially undefined for existing nodes values can be set/get
		by

		.. code-block::

			A[node] = value # set
			value = A[node] # get

		Getting undefined values raises a ValueError removing a node makes all
		its attributes undefined

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.NodeAttribute
			The resulting node attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return NodeAttribute(NodeIntAttribute().setThis(dereference(self._this).attachNodeIntAttribute(stdstring(name)), self._this.get()), int)
		elif ofType == float:
			return NodeAttribute(NodeDoubleAttribute().setThis(dereference(self._this).attachNodeDoubleAttribute(stdstring(name)), self._this.get()), float)
		elif ofType == str:
			return NodeAttribute(NodeStringAttribute().setThis(dereference(self._this).attachNodeStringAttribute(stdstring(name)), self._this.get()), str)

	def getNodeAttribute(self, name, ofType):
		"""
		getNodeAttribute(name, ofType)

		Gets a node attribute that is already attached to the graph and returns it.

		.. code-block::

			A = G.getNodeAttribute("attributeIdentifier", ofType)

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.NodeAttribute
			The resulting node attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return NodeAttribute(NodeIntAttribute().setThis(dereference(self._this).getNodeIntAttribute(stdstring(name)), self._this.get()), int)
		elif ofType == float:
			return NodeAttribute(NodeDoubleAttribute().setThis(dereference(self._this).getNodeDoubleAttribute(stdstring(name)), self._this.get()), float)
		elif ofType == str:
			return NodeAttribute(NodeStringAttribute().setThis(dereference(self._this).getNodeStringAttribute(stdstring(name)), self._this.get()), str)

	def detachNodeAttribute(self, name):
		"""
		detachNodeAttribute(name)

		Detaches a node attribute from the graph.

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name : str
			The distinguished name for the attribute to detach.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")
		dereference(self._this).detachNodeAttribute(stdstring(name))

	def attachEdgeAttribute(self, name, ofType):
		"""
		attachEdgeAttribute(name, ofType)

		Attaches an edge attribute to the graph and returns it.

		.. code-block::

			A = G.attachEdgeAttribute("attributeIdentifier", ofType)

		All values are initially undefined for existing edges values can be set/get by

		.. code-block::

			A[edgeId] = value # set
			value = A[edgeId] # get

		Getting undefined values raises a ValueError removing an edge makes all
		its attributes undefined

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.EdgeAttribute
			The resulting edge attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return EdgeAttribute(EdgeIntAttribute().setThis(dereference(self._this).attachEdgeIntAttribute(stdstring(name)), self._this.get()), int)
		elif ofType == float:
			return EdgeAttribute(EdgeDoubleAttribute().setThis(dereference(self._this).attachEdgeDoubleAttribute(stdstring(name)), self._this.get()), float)
		elif ofType == str:
			return EdgeAttribute(EdgeStringAttribute().setThis(dereference(self._this).attachEdgeStringAttribute(stdstring(name)), self._this.get()), str)


	def getEdgeAttribute(self, name, ofType):
		"""
		getEdgeAttribute(name, ofType)

		Gets an edge attribute that is already attached to the graph and returns it.

		.. code-block::

			A = G.getEdgeAttribute("attributeIdentifier", ofType)

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.EdgeAttribute
			The resulting edge attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return EdgeAttribute(EdgeIntAttribute().setThis(dereference(self._this).getEdgeIntAttribute(stdstring(name)), self._this.get()), int)
		elif ofType == float:
			return EdgeAttribute(EdgeDoubleAttribute().setThis(dereference(self._this).getEdgeDoubleAttribute(stdstring(name)), self._this.get()), float)
		elif ofType == str:
			return EdgeAttribute(EdgeStringAttribute().setThis(dereference(self._this).getEdgeStringAttribute(stdstring(name)), self._this.get()), str)

	def detachEdgeAttribute(self, name):
		"""
		detachEdgeAttribute(name)

		Detaches an edge attribute from the graph.

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name : str
			The distinguished name for the attribute to detach.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")
		dereference(self._this).detachEdgeAttribute(stdstring(name))

	# Mutable operations (require underlying _GraphW)
	def indexEdges(self, bool_t force = False):
		"""
		indexEdges(force = False)

		Assign integer ids to edges.

		Parameters
		----------
		force : bool, optional
			Force re-indexing of edges. Default: False
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot index edges")
		gw.indexEdges(force)

	def addNode(self):
		"""
		addNode()

		Add a new node to the graph and return the node id.

		Returns
		-------
		int
			The id of the new node.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot add nodes")
		return gw.addNode()

	def addNodes(self, numberOfNewNodes):
		"""
		addNodes(numberOfNewNodes)

		Add numberOfNewNodes many new nodes to the graph and return
		the id of the last node added.

		Parameters
		----------
		numberOfNewNodes : int
			Number of nodes to be added.

		Returns
		-------
		int
			The id of the last node added.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot add nodes")
		assert(numberOfNewNodes >= 0)
		return gw.addNodes(numberOfNewNodes)

	def addEdge(self, u, v, w=1.0, addMissing=False, checkMultiEdge=False):
		"""
		addEdge(u, v, w=1.0, addMissing=False, checkMultiEdge=False)

		Insert an undirected edge between the nodes `u` and `v`. If the graph is weighted you can optionally set a weight for this edge. The default weight is 1.0.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float, optional
			Edge weight. Default: 1.0
		addMissing : bool, optional
			Add missing endpoints if necessary (i.e., increase numberOfNodes). Default: False
		checkMultiEdge : bool, optional
			Check if edge already exists, if so, do not insert. Default: False

		Returns
		-------
		bool
			True if edge was added, False otherwise (e.g., if checkMultiEdge and edge exists)
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot add edges")

		if not (gw.hasNode(u) and gw.hasNode(v)):
			if not addMissing:
				raise RuntimeError(
					"Cannot create edge ({0}, {1}) as at least one end point does not exist".format(
						u, v
					)
				)

			k = max(u, v)
			previous_num_nodes = gw.numberOfNodes()
			if k >= gw.upperNodeIdBound():
				gw.addNodes(k - gw.upperNodeIdBound() + 1)
				# Remove nodes that were only created as gap fillers.
				for node in range(previous_num_nodes, gw.numberOfNodes()):
					if node != u and node != v:
						gw.removeNode(node)

			if not gw.hasNode(u):
				gw.restoreNode(u)

			if not gw.hasNode(v):
				gw.restoreNode(v)

		return gw.addEdge(u, v, w, checkMultiEdge)

	def addEdges(self, inputData, addMissing = False, checkMultiEdge = False):
		"""
		addEdges(inputData)

		Inserts edges from several sources based on the type of :code:`inputData`.

		If the graph is undirected, each pair (i,j) in :code:`inputData` is inserted twice twice: once as (i,j) and once as (j,i).

		Parameter :code:`inputData` can be one of the following:

		- scipy.sparse.coo_matrix
		- (data, (i,j)) where data, i and j are of type np.ndarray
		- (i,j) where i and j are of type np.ndarray

		Note
		----
		If only pairs of row and column indices (i,j) are given, each edge is given weight 1.0 (even in case of a weighted graph).

		Parameters
		----------
		inputData : several
			Input data encoded as one of the supported formats.
		addMissing : bool, optional
			Add missing endpoints if necessary (i.e., increase numberOfNodes). Default: False
		checkMultiEdge : bool, optional
			Check if edge is already present in the graph. If detected, do not insert the edge. Default: False
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot add edges")

		cdef cnp.ndarray[cnp.npy_ulong, ndim = 1, mode = 'c'] row, col
		cdef cnp.ndarray[cnp.npy_double, ndim = 1, mode = 'c'] data

		if isinstance(inputData, coo_matrix):
			try:
				row = inputData.row.astype(np.ulong)
				col = inputData.col.astype(np.ulong)
				data = inputData.data.view(np.double)
			except (TypeError, ValueError) as e:
				raise TypeError('invalid input format') from e
		elif isinstance(inputData, tuple) and len(inputData) == 2:
			if isinstance(inputData[1], tuple):
				try:
					row = inputData[1][0].astype(np.ulong)
					col = inputData[1][1].astype(np.ulong)
					data = inputData[0].view(dtype = np.double)
				except (TypeError, ValueError) as e:
					raise TypeError('invalid input format') from e
			else:
				try:
					row = inputData[0].astype(np.ulong)
					col = inputData[1].astype(np.ulong)
					data = np.ones(len(row), dtype = np.double)
				except (TypeError, ValueError) as e:
					raise TypeError('invalid input format') from e
		else:
			raise TypeError('invalid input format')

		cdef int numEdges = len(row)

		if addMissing:
			for i in range(numEdges):
				# Calling Python interface of addEdge due to addMissing support.
				self.addEdge(row[i], col[i], data[i], addMissing, checkMultiEdge)
		else:
			for i in range(numEdges):
				# Calling Cython interface of addEdge directly for higher performance.
				gw.addEdge(row[i], col[i], data[i], checkMultiEdge)

		return self

	def increaseWeight(self, u, v, w):
		"""
		increaseWeight(u, v, w)

		Increase the weight of an edge. If the edge does not exist, it will be inserted.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float
			Edge weight.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot increase weight")
		gw.increaseWeight(u, v, w)
		return self

	def removeNode(self, u):
		"""
		removeNode(u)

		Remove a node `u` and all incident edges from the graph.

		Parameters
		----------
		u : int
			Id of node to be removed.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot remove nodes")
		gw.removeNode(u)

	def restoreNode(self, u):
		"""
		restoreNode(u)

		Restores a previously deleted node `u` with its previous id in the graph.

		Parameters
		----------
		u : int
			The input node.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot restore nodes")
		gw.restoreNode(u)

	def removeEdge(self, u, v):
		"""
		removeEdge(u, v)

		Removes the undirected edge {`u`,`v`}.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot remove edges")
		gw.removeEdge(u, v)

	def removeAllEdges(self):
		"""
		removeAllEdges()

		Removes all the edges in the graph.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot remove edges")
		gw.removeAllEdges()

	def removeSelfLoops(self):
		"""
		removeSelfLoops()

		Removes all self-loops from the graph.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot remove edges")
		gw.removeSelfLoops()

	def removeMultiEdges(self):
		"""
		removeMultiEdges()

		Removes all multi-edges from the graph.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot remove edges")
		gw.removeMultiEdges()

	def swapEdge(self, node s1, node t1, node s2, node t2):
		"""
		swapEdge(s1, t1, s2, t2)

		Changes the edge (s1, t1) into (s1, t2) and the edge (s2, t2) into (s2, t1).

		If there are edge weights or edge ids, they are preserved.

		Note
		----
		No check is performed if the swap is actually possible, i.e. does not generate duplicate edges.

		Parameters
		----------
		s1 : int
			Source node of the first edge.
		t1 : int
			Target node of the first edge.
		s2 : int
			Source node of the second edge.
		t2 : int
			Target node of the second edge.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot swap edges")
		gw.swapEdge(s1, t1, s2, t2)
		return self

	def sortEdges(self):
		"""
		sortEdges()

		Sorts the adjacency arrays by node id. While the running time is linear this
		temporarily duplicates the memory.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot sort edges")
		gw.sortEdges()

	def compactEdges(self):
		"""
		compactEdges()

		Compacts the adjacency arrays by re-using no longer needed slots from
		deleted edges. This is a deprecated operation.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot compact edges")
		gw.compactEdges()

	def setWeight(self, u, v, w):
		"""
		setWeight(u, v, w)

		Set the weight of an edge. If the edge does not exist, it will be inserted.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float
			Edge weight.
		"""
		cdef _GraphW* gw = <_GraphW*>(self._this.get())
		if gw == NULL:
			raise RuntimeError("Graph is read-only (GraphR), cannot set weight")
		gw.setWeight(u, v, w)
		return self

cdef class GraphW:
	"""
	GraphW(n=0, weighted=False, directed=False, edgesIndexed=False)

	A writable graph that extends Graph with mutation operations.
	This class provides all read operations from Graph plus write operations
	like addNode, addEdge, removeNode, removeEdge, etc.

	Parameters
	----------
	n : int, optional
		Number of nodes. Default: 0
	weighted : bool, optional
		If set to True, the graph can have edge weights other than 1.0. Default: False
	directed : bool, optional
		If set to True, the graph will be directed. Default: False
	edgesIndexed : bool, optional
		If set to True, the graph's edges will be indexed. Default: False
	"""

	def __cinit__(self, n=0, bool_t weighted=False, bool_t directed=False, bool_t edgesIndexed=False):
		if isinstance(n, Graph):
			self._this = _GraphW(dereference((<Graph>n)._this))
		elif isinstance(n, GraphW):
			self._this = _GraphW((<GraphW>n)._this)
		else:
			self._this = _GraphW(<count>n, weighted, directed, edgesIndexed)

	cdef setThis(self, _GraphW& other):
		swap[_GraphW](self._this, other)
		return self

	def __copy__(self):
		"""
		Generates a copy of the graph
		"""
		return GraphW().setThis(_GraphW(self._this))

	def __deepcopy__(self, memo):
		"""
		Generates a (deep) copy of the graph
		"""
		return GraphW().setThis(_GraphW(self._this))

	def __str__(self):
		return "NetworKit.GraphW(n={0}, m={1})".format(self.numberOfNodes(), self.numberOfEdges())

	# Inherit all read operations from Graph by delegation
	def numberOfNodes(self):
		return self._this.numberOfNodes()

	def numberOfEdges(self):
		return self._this.numberOfEdges()

	def upperNodeIdBound(self):
		return self._this.upperNodeIdBound()

	def upperEdgeIdBound(self):
		return self._this.upperEdgeIdBound()

	def degree(self, u):
		return self._this.degree(u)

	def degreeIn(self, u):
		return self._this.degreeIn(u)

	def degreeOut(self, u):
		return self._this.degreeOut(u)

	def weightedDegree(self, u, countSelfLoopsTwice=False):
		return self._this.weightedDegree(u, countSelfLoopsTwice)

	def weightedDegreeIn(self, u, countSelfLoopsTwice=False):
		return self._this.weightedDegreeIn(u, countSelfLoopsTwice)

	def isIsolated(self, u):
		return self._this.isIsolated(u)

	def hasNode(self, u):
		return self._this.hasNode(u)

	def hasEdge(self, u, v):
		return self._this.hasEdge(u, v)

	def weight(self, u, v):
		return self._this.weight(u, v)

	def isWeighted(self):
		return self._this.isWeighted()

	def isDirected(self):
		return self._this.isDirected()

	def totalEdgeWeight(self):
		return self._this.totalEdgeWeight()

	def numberOfSelfLoops(self):
		return self._this.numberOfSelfLoops()

	def checkConsistency(self):
		return self._this.checkConsistency()

	def hasEdgeIds(self):
		return self._this.hasEdgeIds()

	def edgeId(self, node u, node v):
		return self._this.edgeId(u, v)

	# Write operations - moved from Graph
	def indexEdges(self, bool_t force = False):
		"""
		indexEdges(force = False)

		Assign integer ids to edges.

		Parameters
		----------
		force : bool, optional
			Force re-indexing of edges. Default: False
		"""
		self._this.indexEdges(force)

	def addNode(self):
		"""
		addNode()

		Add a new node to the graph and return it.

		Returns
		-------
		int
			The new node.
		"""
		return self._this.addNode()

	def addNodes(self, numberOfNewNodes):
		"""
		addNodes(numberOfNewNodes)

		Add numberOfNewNodes many new nodes to the graph and return
		the id of the last node added.

		Parameters
		----------
		numberOfNewNodes : int
			Number of nodes to be added.

		Returns
		-------
		int
			The id of the last node added.
		"""
		assert(numberOfNewNodes >= 0)
		return self._this.addNodes(numberOfNewNodes)

	def removeNode(self, u):
		"""
		removeNode(u)

		Remove a node `u` and all incident edges from the graph.

		Incoming as well as outgoing edges will be removed.

		Parameters
		----------
		u : int
			Id of node to be removed.
		"""
		self._this.removeNode(u)

	def restoreNode(self, u):
		"""
		restoreNode(u)

		Restores a previously deleted node `u` with its previous id in the graph.

		Parameters
		----------
		u : int
			The input node.
		"""
		self._this.restoreNode(u)

	def addEdge(self, u, v, w=1.0, addMissing = False, checkMultiEdge = False):
		"""
		addEdge(u, v, w=1.0, addMissing=False, checkMultiEdge=False)

		Insert an undirected edge between the nodes `u` and `v`. If the graph is weighted you can optionally set a weight for this edge.
		The default weight is 1.0. If one or both end-points do not exists and addMissing is set, they are silently added.

		Note
		----
		By default it is not checked whether this edge already exists, thus it is possible to create multi-edges. Multi-edges are not supported and will NOT be
		handled consistently by the graph data structure. To enable set :code:`checkMultiEdge` to True. Note that this increases the runtime of the function by O(max(deg(u), deg(v))).

	 	Parameters
	 	----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float, optional
			Edge weight. Default: 1.0
		addMissing : bool, optional
			Add missing endpoints if necessary (i.e., increase numberOfNodes). Default: False
		checkMultiEdge : bool, optional
			Check if edge is already present in the graph. If detected, do not insert the edge. Default: False

		Returns
		-------
		bool
			Indicates whether the edge has been added. Is `False` in case :code:`checkMultiEdge` is set to `True` and the new edge would have been a multi-edge.
		"""
		if not (self._this.hasNode(u) and self._this.hasNode(v)):
			if not addMissing:
				raise RuntimeError("Cannot create edge ({0}, {1}) as at least one end point does not exist".format(u,v))

			k = max(u, v)
			previous_num_nodes = self._this.numberOfNodes()
			if k >= self._this.upperNodeIdBound():
				self._this.addNodes(k - self._this.upperNodeIdBound() + 1)
				# removing the nodes that have not been added by this edge
				for node in range(previous_num_nodes, self._this.numberOfNodes()):
					if node != u and node != v:
						self._this.removeNode(node)

			if not self._this.hasNode(u):
				self._this.restoreNode(u)

			if not self._this.hasNode(v):
				self._this.restoreNode(v)

		return self._this.addEdge(u, v, w, checkMultiEdge)

	def addEdges(self, inputData, addMissing = False, checkMultiEdge = False):
		"""
		addEdges(inputData)

		Inserts edges from several sources based on the type of :code:`inputData`.

		If the graph is undirected, each pair (i,j) in :code:`inputData` is inserted twice twice: once as (i,j) and once as (j,i).

		Parameter :code:`inputData` can be one of the following:

		- scipy.sparse.coo_matrix
		- (data, (i,j)) where data, i and j are of type np.ndarray
		- (i,j) where i and j are of type np.ndarray

		Note
		----
		If only pairs of row and column indices (i,j) are given, each edge is given weight 1.0 (even in case of a weighted graph).

		Parameters
		----------
		inputData : several
			Input data encoded as one of the supported formats.
		addMissing : bool, optional
			Add missing endpoints if necessary (i.e., increase numberOfNodes). Default: False
		checkMultiEdge : bool, optional
			Check if edge is already present in the graph. If detected, do not insert the edge. Default: False
		"""

		cdef cnp.ndarray[cnp.npy_ulong, ndim = 1, mode = 'c'] row, col
		cdef cnp.ndarray[cnp.npy_double, ndim = 1, mode = 'c'] data

		if isinstance(inputData, coo_matrix):
			try:
				row = inputData.row.astype(np.ulong)
				col = inputData.col.astype(np.ulong)
				data = inputData.data.view(np.double)
			except (TypeError, ValueError) as e:
				raise TypeError('invalid input format') from e
		elif isinstance(inputData, tuple) and len(inputData) == 2:
			if isinstance(inputData[1], tuple):
				try:
					row = inputData[1][0].astype(np.ulong)
					col = inputData[1][1].astype(np.ulong)
					data = inputData[0].view(dtype = np.double)
				except (TypeError, ValueError) as e:
					raise TypeError('invalid input format') from e
			else:
				try:
					row = inputData[0].astype(np.ulong)
					col = inputData[1].astype(np.ulong)
					data = np.ones(len(row), dtype = np.double)
				except (TypeError, ValueError) as e:
					raise TypeError('invalid input format') from e
		else:
			raise TypeError('invalid input format')

		cdef int numEdges = len(row)

		if addMissing:
			for i in range(numEdges):
				# Calling Python interface of addEdge due to addMissing support.
				self.addEdge(row[i], col[i], data[i], addMissing, checkMultiEdge)
		else:
			for i in range(numEdges):
				# Calling Cython interface of addEdge directly for higher performance.
				self._this.addEdge(row[i], col[i], data[i], checkMultiEdge)

		return self

	def setWeight(self, u, v, w):
		"""
		setWeight(u, v, w)

		Set the weight of an edge. If the edge does not exist, it will be inserted.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float
			Edge weight.
		"""
		self._this.setWeight(u, v, w)
		return self

	def increaseWeight(self, u, v, w):
		"""
		increaseWeight(u, v, w)

		Increase the weight of an edge. If the edge does not exist, it will be inserted.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		w : float
			Edge weight.
		"""
		self._this.increaseWeight(u, v, w)
		return self

	def removeEdge(self, u, v):
		"""
		removeEdge(u, v)

		Removes the undirected edge {`u`,`v`}.

		Parameters
		----------
		u : int
			Endpoint of edge.
		v : int
			Endpoint of edge.
		"""
		self._this.removeEdge(u, v)
		return self

	def removeAllEdges(self):
		"""
		removeAllEdges()

		Removes all the edges in the graph.
		"""
		self._this.removeAllEdges()

	def removeSelfLoops(self):
		"""
		removeSelfLoops()

		Removes all self-loops from the graph.
		"""
		self._this.removeSelfLoops()

	def removeMultiEdges(self):
		"""
		removeMultiEdges()

		Removes all multi-edges from the graph.
		"""
		self._this.removeMultiEdges()

	def swapEdge(self, node s1, node t1, node s2, node t2):
		"""
		swapEdge(s1, t1, s2, t2)

		Changes the edge (s1, t1) into (s1, t2) and the edge (s2, t2) into (s2, t1).

		If there are edge weights or edge ids, they are preserved.

		Note
		----
		No check is performed if the swap is actually possible, i.e. does not generate duplicate edges.

		Parameters
		----------
		s1 : int
			Source node of the first edge.
		t1 : int
			Target node of the first edge.
		s2 : int
			Source node of the second edge.
		t2 : int
			Target node of the second edge.
		"""
		self._this.swapEdge(s1, t1, s2, t2)
		return self

	def sortEdges(self):
		"""
		sortEdges()

		Sorts the adjacency arrays by node id. While the running time is linear this
		temporarily duplicates the memory.
		"""
		self._this.sortEdges()

	def compactEdges(self):
		"""
		compactEdges()

		Compacts the adjacency arrays by re-using no longer needed slots from
		deleted edges. This is a deprecated operation.
		"""
		self._this.compactEdges()

	# Iterator and callback methods
	def forNodes(self, object callback):
		"""
		forNodes(callback)

		Experimental node iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameter node.
		"""
		cdef NodeCallbackWrapper* wrapper
		try:
			wrapper = new NodeCallbackWrapper(callback)
			self._this.forNodes[NodeCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forNodesInRandomOrder(self, object callback):
		"""
		forNodesInRandomOrder(callback)

		Experimental node iterator interface

		Parameters:
		-----------
		callback : object
			Any callable object that takes the parameter node.
		"""
		cdef NodeCallbackWrapper* wrapper
		try:
			wrapper = new NodeCallbackWrapper(callback)
			self._this.forNodesInRandomOrder[NodeCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forNodePairs(self, object callback):
		"""
		forNodePairs(callback)

		Experimental node pair iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameters tuple(int, int).
			Parameter list refering to (node id, node id).
		"""
		cdef NodePairCallbackWrapper* wrapper
		try:
			wrapper = new NodePairCallbackWrapper(callback)
			self._this.forNodePairs[NodePairCallbackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forEdges(self, object callback):
		"""
		forEdges(callback)

		Experimental edge iterator interface

		Parameters
		----------
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			self._this.forEdges[EdgeCallBackWrapper](dereference(wrapper))
		finally:
			del wrapper

	def forEdgesOf(self, node u, object callback):
		"""
		forEdgesOf(u, callback)

		Experimental incident (outgoing) edge iterator interface

		Parameters
		----------
		u : int
			The node of which incident edges shall be passed to the callback
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			self._this.forEdgesOf[EdgeCallBackWrapper](u, dereference(wrapper))
		finally:
			del wrapper

	def forInEdgesOf(self, node u, object callback):
		"""
		forInEdgesOf(u, callback)

		Experimental incident edge iterator interface

		Parameters
		----------
		u : int
			The node of which incident edges shall be passed to the callback
		callback : object
			Any callable object that takes the parameter tuple(int, int, float, int).
			Parameter list refering to (node id, node id, edge weight, edge id).
		"""
		cdef EdgeCallBackWrapper* wrapper
		try:
			wrapper = new EdgeCallBackWrapper(callback)
			self._this.forInEdgesOf[EdgeCallBackWrapper](u, dereference(wrapper))
		finally:
			del wrapper

	def iterNodes(self):
		"""
		iterNodes()

		Iterates over the nodes of the graph.
		"""
		cdef _NodeRange node_range = self._this.nodeRange()
		cdef _NodeIterator it = node_range.begin()
		cdef _NodeIterator end_it = node_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterEdges(self):
		"""
		iterEdges()

		Iterates over the edges of the graph.

		For each node u in the graph in ascending node id order,
		the iterator yields the out-edges of u in directed graphs
		and the edges (u,v) in which u < v for undirected graphs.

		It does not follow the order of edge ids (if present).
		"""
		cdef _EdgeRange edge_range = self._this.edgeRange()
		cdef _EdgeIterator it = edge_range.begin()
		cdef _EdgeIterator end_it = edge_range.end()
		while it != end_it:
			yield dereference(it).u, dereference(it).v
			preincrement(it)

	def iterEdgesWeights(self):
		"""
		iterEdgeWeights()

		Iterates over the edges of the graph and their weights.
		"""
		cdef _EdgeWeightRange edge_weight_range = self._this.edgeWeightRange()
		cdef _EdgeWeightIterator it = edge_weight_range.begin()
		cdef _EdgeWeightIterator end_it = edge_weight_range.end()
		while it != end_it:
			yield dereference(it).u, dereference(it).v, dereference(it).weight
			preincrement(it)

	def iterNeighbors(self, u):
		"""
		iterNeighbors(u)

		Iterates over a range of the neighbors of a node.

		Parameters
		----------
		u : int
			The input node.
		"""
		cdef _OutNeighborRange neighbor_range = self._this.neighborRange(u)
		cdef _NeighborIterator it = neighbor_range.begin()
		cdef _NeighborIterator end_it = neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterInNeighbors(self, u):
		"""
		iterInNeighbors(u)

		Iterates over a range of the in-neighbors of a node.

		Parameters
		----------
		u : int
			The input node.
		"""
		cdef _InNeighborRange in_neighbor_range = self._this.inNeighborRange(u)
		cdef _NeighborIterator it = in_neighbor_range.begin()
		cdef _NeighborIterator end_it = in_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterNeighborsWeights(self, u):
		"""
		iterNeighborsWeights(u)

		Iterates over a range of the neighbors of a node including the edge weights.
		The iterator is not safe to use with unweighted graphs. To avoid unsafe behavior
		a runtime error will be thrown.

		Parameters
		----------
		u : int
			The input node.
		"""
		if not self._this.isWeighted():
			raise RuntimeError("iterNeighborsWeights: Use this iterator only on weighted graphs.")

		cdef _OutNeighborWeightRange weight_neighbor_range = self._this.weightNeighborRange(u)
		cdef _NeighborWeightIterator it = weight_neighbor_range.begin()
		cdef _NeighborWeightIterator end_it = weight_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	def iterInNeighborsWeights(self, u):
		"""
		iterInNeighborsWeights(u)

		Iterates over a range of the in-neighbors of a node including the edge weights.
		The iterator is not safe to use with unweighted graphs. To avoid unsafe behavior
		a runtime error will be thrown.

		Parameters
		----------
		u : int
			The input node.
		"""
		if not self._this.isWeighted():
			raise RuntimeError("iterInNeighborsWeights: Use this iterator only on weighted graphs.")

		cdef _InNeighborWeightRange weight_in_neighbor_range = self._this.weightInNeighborRange(u)
		cdef _NeighborWeightIterator it = weight_in_neighbor_range.begin()
		cdef _NeighborWeightIterator end_it = weight_in_neighbor_range.end()
		while it != end_it:
			yield dereference(it)
			preincrement(it)

	# Node and Edge Attribute methods
	def attachNodeAttribute(self, name, ofType):
		"""
		attachNodeAttribute(name, ofType)

		Attaches a node attribute to the graph and returns it.

		.. code-block::

			A = G.attachNodeAttribute("attributeIdentifier", ofType)

		All values are initially undefined for existing nodes values can be set/get
		by

		.. code-block::

			A[node] = value # set
			value = A[node] # get

		Getting undefined values raises a ValueError removing a node makes all
		its attributes undefined

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.NodeAttribute
			The resulting node attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return NodeAttribute(NodeIntAttribute().setThis(self._this.attachNodeIntAttribute(stdstring(name)), &self._this), int)
		elif ofType == float:
			return NodeAttribute(NodeDoubleAttribute().setThis(self._this.attachNodeDoubleAttribute(stdstring(name)), &self._this), float)
		elif ofType == str:
			return NodeAttribute(NodeStringAttribute().setThis(self._this.attachNodeStringAttribute(stdstring(name)), &self._this), str)

	def getNodeAttribute(self, name, ofType):
		"""
		getNodeAttribute(name, ofType)

		Gets a node attribute that is already attached to the graph and returns it.

		.. code-block::

			A = G.getNodeAttribute("attributeIdentifier", ofType)

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.NodeAttribute
			The resulting node attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return NodeAttribute(NodeIntAttribute().setThis(self._this.getNodeIntAttribute(stdstring(name)), &self._this), int)
		elif ofType == float:
			return NodeAttribute(NodeDoubleAttribute().setThis(self._this.getNodeDoubleAttribute(stdstring(name)), &self._this), float)
		elif ofType == str:
			return NodeAttribute(NodeStringAttribute().setThis(self._this.getNodeStringAttribute(stdstring(name)), &self._this), str)

	def detachNodeAttribute(self, name):
		"""
		detachNodeAttribute(name)

		Detaches a node attribute from the graph.

		Notes
		-----
		Using node attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name : str
			The distinguished name for the attribute to detach.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")
		self._this.detachNodeAttribute(stdstring(name))

	def attachEdgeAttribute(self, name, ofType):
		"""
		attachEdgeAttribute(name, ofType)

		Attaches an edge attribute to the graph and returns it.

		.. code-block::

			A = G.attachEdgeAttribute("attributeIdentifier", ofType)

		All values are initially undefined for existing edges values can be set/get by

		.. code-block::

			A[edgeId] = value # set
			value = A[edgeId] # get

		Getting undefined values raises a ValueError removing an edge makes all
		its attributes undefined

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.EdgeAttribute
			The resulting edge attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return EdgeAttribute(EdgeIntAttribute().setThis(self._this.attachEdgeIntAttribute(stdstring(name)), &self._this), int)
		elif ofType == float:
			return EdgeAttribute(EdgeDoubleAttribute().setThis(self._this.attachEdgeDoubleAttribute(stdstring(name)), &self._this), float)
		elif ofType == str:
			return EdgeAttribute(EdgeStringAttribute().setThis(self._this.attachEdgeStringAttribute(stdstring(name)), &self._this), str)


	def getEdgeAttribute(self, name, ofType):
		"""
		getEdgeAttribute(name, ofType)

		Gets an edge attribute that is already attached to the graph and returns it.

		.. code-block::

			A = G.getEdgeAttribute("attributeIdentifier", ofType)

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name   : str
			Name for this attribute
		ofType : type
			Type of the attribute (either int, float, or str)

		Returns
		-------
		networkit.graph.EdgeAttribute
			The resulting edge attribute container.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")

		if ofType == int:
			return EdgeAttribute(EdgeIntAttribute().setThis(self._this.getEdgeIntAttribute(stdstring(name)), &self._this), int)
		elif ofType == float:
			return EdgeAttribute(EdgeDoubleAttribute().setThis(self._this.getEdgeDoubleAttribute(stdstring(name)), &self._this), float)
		elif ofType == str:
			return EdgeAttribute(EdgeStringAttribute().setThis(self._this.getEdgeStringAttribute(stdstring(name)), &self._this), str)

	def detachEdgeAttribute(self, name):
		"""
		detachEdgeAttribute(name)

		Detaches an edge attribute from the graph.

		Notes
		-----
		Using edge attributes is in experimental state. The API may change in future updates.

		Parameters
		----------
		name : str
			The distinguished name for the attribute to detach.
		"""
		if not isinstance(name, str):
			raise Exception("Attribute name has to be a string")
		self._this.detachEdgeAttribute(stdstring(name))

def GraphFromCoo(inputData, n=0, bool_t weighted=False, bool_t directed=False, bool_t edgesIndexed=False):
	"""
	graphFromInputData(inputData, n=0, bool_t weighted=False, bool_t directed=False, bool_t edgesIndexed=False):

	Creates a graph based on :code:`inputData` (edge data). Input data is given in triplet format (also known
	as ijk or coo format). See here for more details: https://docs.scipy.org/doc/scipy/reference/generated/scipy.sparse.coo_array.html

	If the resulting graph is undirected (default case), each pair (i,j) in :code:`inputData` is
	inserted twice twice: once as (i,j) and once as (j,i).

	Parameter :code:`inputData` can be one of the following:

	- scipy.sparse.coo_matrix
	- (data, (i,j)) where data, i and j are of type np.ndarray
	- (i,j) where i and j are of type np.ndarray

	Note
	----
	- If only pairs of row and column indices (i,j) are given, each edge is given weight 1.0 (even in case of a weighted graph).
	- There is no check if :code:`n` is the correct size. If the parameter is used, make sure that it is at least the
	maximum index from the coordinate data.

	Parameters
	----------
	inputData : several
		Input data encoded as one of the supported formats.
	n : int, optional
		Number of nodes for the created graph. If n is not given, the nodes are added on the fly during building
		of the graph. For better performance, it is advised to correctly set the number of nodes. Default: 0
	weighted : bool, optional
		If set to True, the graph can have edge weights other than 1.0. Default: False
	directed : bool, optional
		If set to True, the graph will be directed. Default: False
	edgesIndexed : bool, optional
		If set to True, the graph's edges will be indexed. Default: False
	"""
	cdef GraphW result
	result = GraphW(n, weighted, directed, edgesIndexed)

	if n > 0:
		result.addEdges(inputData, addMissing = False, checkMultiEdge = False)
	else:
		result.addEdges(inputData, addMissing = True, checkMultiEdge = False)

	return result

# The following 3 classes NodeIntAttribute, NodeDoubleAttribute and
# NodeStringAttribute are helper classes which cannot be generalized because
# they map to different C++ classes even if these are generated from the same
# C++ template - this results in some unpleasant code duplication.
# The generic (pure python) wrapper class for the user is NodeAttribute

cdef class NodeIntAttribute:

	cdef setThis(self, _NodeIntAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def __getitem__(self, node):
		try:
			value = self._this.get(node)
		except Exception as e:
			raise ValueError(str(e))
		return value

	def getName(self):
		return self._this.getName()

	def __setitem__(self, node, value):
		try:
			self._this.set(node, value)
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))

		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))


cdef class NodeDoubleAttribute:
	cdef setThis(self, _NodeDoubleAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def __getitem__(self, node):
		try:
			value = self._this.get(node)
		except Exception as e:
			raise ValueError(str(e))
		return value

	def getName(self):
		return self._this.getName()

	def __setitem__(self, node, value):
		try:
			self._this.set(node, value)
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))
		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))

cdef class NodeStringAttribute:

	cdef setThis(self, _NodeStringAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def getName(self):
		return self._this.getName()

	def __getitem__(self, node):
		try:
			value = pystring(self._this.get(node))
		except Exception as e:
			raise ValueError(str(e))
		return value

	def __setitem__(self, node, value):
		try:
			self._this.set(node, stdstring(value))
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))
		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		val = (val[0], pystring(val[1]))
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))

class NodeAttribute:
	"""
	Generic class for node attributes returned by networkit.graph.attachNodeAttribute().
	Example of attaching an int attribute to a graph g:

	.. code-block::

		att = g.attachNodeAttribute("name", int)`

	Set/get attributes of a single node 'u' with the [] operator:

	.. code-block::

		att[u] = 0
		att_val = att[u] # 'att_val' is 0

	Iterate over all the values of an attribute:

	.. code-block::

		for u, val in att:
			# The attribute value of node `u` is `val`.

	Notes
	-----
	Using node attributes is in experimental state. The API may change in future updates.
	"""

	def __init__(self, typedNodeAttribute, type):
		self.attr = typedNodeAttribute
		self.type = type

	def getName(self):
		return self.attr.getName()

	def __getitem__(self, node):
		return self.attr[node]

	def __setitem__(self, node, value):
		if not isinstance(value, self.type):
			raise Exception("Wrong Attribute type")
		self.attr[node] = value

	def __iter__(self):
		self._iter = iter(self.attr)
		return self

	def __next__(self):
		return next(self._iter)

	def write(self, path: str):
		return self.attr.write(path)

	def read(self, path: str):
		return self.attr.read(path)


# The following 3 classes EdgeIntAttribute, EdgeDoubleAttribute and
# EdgeStringAttribute are helper classes which cannot be generalized because
# they map to different C++ classes even if these are generated from the same
# C++ template - this results in some unpleasant code duplication.
# The generic (pure python) wrapper class for the user is EdgeAttribute

cdef class EdgeIntAttribute:

	cdef setThis(self, _EdgeIntAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def __getitem__(self, edgeIdORnodePair):
		try:
			u, v = edgeIdORnodePair
			try:
				return self._this.get2(u, v)
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			return self._this.get(edgeIdORnodePair)
		except Exception as e:
			raise ValueError(str(e))

	def __setitem__(self, edgeIdORnodePair, value):
		try:
			u, v = edgeIdORnodePair
			try:
				self._this.set2(u,v,value)
				return
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			self._this.set(edgeIdORnodePair, value)
			return
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))

		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))

cdef class EdgeDoubleAttribute:
	cdef setThis(self, _EdgeDoubleAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def __getitem__(self, edgeIdORnodePair):
		try:
			u, v = edgeIdORnodePair
			try:
				return self._this.get2(u, v)
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			return self._this.get(edgeIdORnodePair)
		except Exception as e:
			raise ValueError(str(e))

	def __setitem__(self, edgeIdORnodePair, value):
		try:
			u, v = edgeIdORnodePair
			try:
				self._this.set2(u,v,value)
				return
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			self._this.set(edgeIdORnodePair, value)
			return
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))
		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))

cdef class EdgeStringAttribute:

	cdef setThis(self, _EdgeStringAttribute& other, _Graph* G):
		self._this.swap(other)
		return self

	def __getitem__(self, edgeIdORnodePair):
		try:
			u, v = edgeIdORnodePair
			try:
				return pystring(self._this.get2(u, v))
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			return pystring(self._this.get(edgeIdORnodePair))
		except Exception as e:
			raise ValueError(str(e))

	def __setitem__(self, edgeIdORnodePair, value):
		try:
			u, v = edgeIdORnodePair
			try:
				self._this.set2(u, v, stdstring(value))
				return
			except Exception as e:
				raise ValueError(str(e))
		except TypeError:
			pass
		try:
			self._this.set(edgeIdORnodePair, stdstring(value))
			return
		except Exception as e:
			raise ValueError(str(e))

	def __iter__(self):
		try:
			self._iter = self._this.begin()
		except Exception as e:
			raise ValueError(str(e))
		self._stopiter = self._this.end()
		return self

	def __next__(self):
		if self._iter == self._stopiter:
			raise StopIteration()
		val = dereference(self._iter)
		val = (val[0], pystring(val[1]))
		preincrement(self._iter)
		return val

	def write(self, path: str):
		return self._this.write(stdstring(path))

	def read(self, path: str):
		return self._this.read(stdstring(path))

class EdgeAttribute:
	"""
	Generic class for edge attributes returned by networkit.graph.attachEdgeAttribute().
	Example of attaching an int attribute to a graph g:

	.. code-block::

		att = g.attachEdgeAttribute("name", int)`

	Set/get attributes of a single edgeId 'eid' with the [] operator:

	.. code-block::

		att[eid] = 0
		att_val = att[eid] # 'att_val' is 0

	Iterate over all the values of an attribute:

	.. code-block::

		for eid, val in att:
			# The attribute value of edge `eid` is `val`.

	Notes
	-----
	Using edge attributes is in experimental state. The API may change in future updates.
	"""

	def __init__(self, typedEdgeAttribute, type):
		self.attr = typedEdgeAttribute
		self.type = type

	def __getitem__(self, edgeIdORnodePair):
		return self.attr[edgeIdORnodePair]

	def __setitem__(self, edgeIdORnodePair, value):
		if not isinstance(value, self.type):
			raise Exception("Wrong Attribute type")
		self.attr[edgeIdORnodePair] = value

	def __iter__(self):
		self._iter = iter(self.attr)
		return self

	def __next__(self):
		return next(self._iter)

	def write(self, path: str):
		return self.attr.write(path)

	def read(self, path: str):
		return self.attr.read(path)


cdef cppclass EdgeCallBackWrapper:
	void* callback
	__init__(object callback):
		this.callback = <void*>callback
	void cython_call_operator(node u, node v, edgeweight w, edgeid eid):
		cdef bool_t error = False
		cdef string message
		try:
			(<object>callback)(u, v, w, eid)
		except Exception as e:
			error = True
			message = stdstring("An Exception occurred, aborting execution of iterator: {0}".format(e))
		if (error):
			throw_runtime_error(message)

cdef cppclass NodeCallbackWrapper:
	void* callback
	__init__(object callback):
		this.callback = <void*>callback
	void cython_call_operator(node u):
		cdef bool_t error = False
		cdef string message
		try:
			(<object>callback)(u)
		except Exception as e:
			error = True
			message = stdstring("An Exception occurred, aborting execution of iterator: {0}".format(e))
		if (error):
			throw_runtime_error(message)

cdef cppclass NodeDistCallbackWrapper:
	void* callback
	__init__(object callback):
		this.callback = <void*>callback
	void cython_call_operator(node u, count dist):
		cdef bool_t error = False
		cdef string message
		try:
			(<object>callback)(u, dist)
		except Exception as e:
			error = True
			message = stdstring("An Exception occurred, aborting execution of iterator: {0}".format(e))
		if (error):
			throw_runtime_error(message)

cdef cppclass NodePairCallbackWrapper:
	void* callback
	__init__(object callback):
		this.callback = <void*>callback
	void cython_call_operator(node u, node v):
		cdef bool_t error = False
		cdef string message
		try:
			(<object>callback)(u, v)
		except Exception as e:
			error = True
			message = stdstring("An Exception occurred, aborting execution of iterator: {0}".format(e))
		if (error):
			throw_runtime_error(message)

cdef class SpanningForest:
	"""
	SpanningForest(G, nodes)

	Generates a spanning forest for a given graph

	Parameters
	----------
	G : networkit.Graph
		The input graph.
	nodes : list(int)
		A subset of nodes of `G` which induce the subgraph.
	"""
	cdef _SpanningForest* _this
	cdef Graph _G

	def __cinit__(self, Graph G not None):
		self._G = G
		self._this = new _SpanningForest(dereference(G._this))


	def __dealloc__(self):
		del self._this

	def run(self):
		"""
		run()

		Executes the algorithm.
		"""
		dereference(self._this).run()
		return self

	def getForest(self):
		"""
		getForest()

		Returns the spanning forest.

		Returns
		-------
		networkit.Graph
			The computed spanning forest.
		"""
		return Graph().setThisFromGraphW(dereference(self._this).getForest())

cdef class RandomMaximumSpanningForest(Algorithm):
	"""
	RandomMaximumSpanningForest(G, attributes)

	Computes a random maximum-weight spanning forest using Kruskal's algorithm by randomizing the order of edges of the same weight.

	Parameters
	----------
	G : networkit.Graph
		The input graph.
	attribute : list(int) or list(float)
		If given, this edge attribute is used instead of the edge weights.
	"""

	def __cinit__(self, Graph G not None, vector[double] attribute = vector[double]()):
		self._G = G
		if attribute.empty():
			self._this = new _RandomMaximumSpanningForest(dereference(G._this))
		else:
			self._attribute = move(attribute)
			self._this = new _RandomMaximumSpanningForest(dereference(G._this), self._attribute)

	def getMSF(self, bool_t move):
		"""
		getMSF(move)

		Gets the calculated maximum-weight spanning forest as graph.

		Parameters
		----------
		move : bool
			If the graph shall be moved out of the algorithm instance.

		Returns
		-------
		networkit.Graph
			The calculated maximum-weight spanning forest.
		"""
		return Graph().setThisFromGraphW((<_RandomMaximumSpanningForest*>(self._this)).getMSF(move))

	def getAttribute(self, bool_t move = False):
		"""
		getAttribute(move=False)

		Get a bool attribute that indicates for each edge if it is part of the calculated maximum-weight spanning forest.
		This attribute is only calculated and can thus only be request if the supplied graph has edge ids.

		Parameters
		----------
		move : bool, optional
			If the attribute shall be moved out of the algorithm instance. Default: False

		Returns
		-------
		list(bool)
			The list with the bool attribute for each edge.
		"""
		return (<_RandomMaximumSpanningForest*>(self._this)).getAttribute(move)

	def inMSF(self, node u, node v = _none):
		"""
		inMSF(u, v = None)

		Checks if the edge (u, v) or the edge with id u is part of the calculated maximum-weight spanning forest.

		Parameters
		----------
		u : int
			The first node of the edge to check or the edge id of the edge to check.
		v : int, optional
			The second node of the edge to check (only if u is not an edge id). Default: None

		Returns
		-------
		bool
			If the edge is part of the calculated maximum-weight spanning forest.
		"""
		if v == _none:
			return (<_RandomMaximumSpanningForest*>(self._this)).inMSF(u)
		else:
			return (<_RandomMaximumSpanningForest*>(self._this)).inMSF(u, v)

cdef class UnionMaximumSpanningForest(Algorithm):
	"""
	UnionMaximumSpanningForest(G, attribute)

	Union maximum-weight spanning forest algorithm, computes the union of all maximum-weight spanning forests using Kruskal's algorithm.

	Parameters
	----------
	G : networkit.Graph
		The input graph.
	attribute : list(int) or list(float)
		If given, this edge attribute is used instead of the edge weights.
	"""

	def __cinit__(self, Graph G not None, vector[double] attribute = vector[double]()):
		self._G = G

		if attribute.empty():
			self._this = new _UnionMaximumSpanningForest(dereference(G._this))
		else:
			self._this = new _UnionMaximumSpanningForest(dereference(G._this), attribute)

	def getUMSF(self, bool_t move = False):
		"""
		getUMSF(move=False)

		Gets the union of all maximum-weight spanning forests as graph.

		Parameters
		----------
		move : bool, optional
			If the graph shall be moved out of the algorithm instance. Default: False

		Returns
		-------
		networkit.Graph
			The calculated union of all maximum-weight spanning forests.
		"""
		return Graph().setThisFromGraphW((<_UnionMaximumSpanningForest*>(self._this)).getUMSF(move))

	def getAttribute(self, bool_t move = False):
		"""
		getAttribute(move=False)

		Get a bool attribute that indicates for each edge if it is part of any maximum-weight spanning forest.

		This attribute is only calculated and can thus only be request if the supplied graph has edge ids.

		Parameters
		----------
		move : bool, optional
			If the attribute shall be moved out of the algorithm instance. Default: False

		Returns
		-------
		list(bool)
			The list with the bool attribute for each edge.
		"""
		return (<_UnionMaximumSpanningForest*>(self._this)).getAttribute(move)

	def inUMST(self, node u, node v = _none):
		"""
		inUMST(u, v=None)

		Checks if the edge (u, v) or the edge with id u is part of any maximum-weight spanning forest.

		Parameters
		----------
		u : int
			The first node of the edge to check or the edge id of the edge to check.
		v : int, optional
			The second node of the edge to check (only if u is not an edge id). Default: None

		Returns
		-------
		bool
			If the edge is part of any maximum-weight spanning forest.
		"""
		if v == _none:
			return (<_UnionMaximumSpanningForest*>(self._this)).inUMSF(u)
		else:
			return (<_UnionMaximumSpanningForest*>(self._this)).inUMSF(u, v)
